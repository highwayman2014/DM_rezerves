Процедура СнятьРезервПодЗаказНаряд(Отказ, РежимПроведения)
	
	Если ЗначениеЗаполнено(ЭтотОбъект.ДокументОснование) 
		И ЭтотОбъект.ДокументОснование.Дата < '20210801' Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЗаказНаряд", ЭтотОбъект.ДокументОснование);
	Запрос.УстановитьПараметр("Перемещение", ЭтотОбъект.Ссылка);
	
	Запрос.Текст = 
	// +++ Инфостарт #2666 28.07.2021 Снятие резерва происходит только по складам из регистра "Склады давальческого сырья КРШ"
	"ВЫБРАТЬ
	|	дмСкладыДавальческогоСырьяПоПодразделениямКрш.СкладДавальческогоСырья КАК СкладДавальческогоСырья
	|ПОМЕСТИТЬ ВТСкладыДС
	|ИЗ
	|	РегистрСведений.дмСкладыДавальческогоСырьяПоПодразделениямКрш КАК дмСкладыДавальческогоСырьяПоПодразделениямКрш
	|
	|СГРУППИРОВАТЬ ПО
	|	дмСкладыДавальческогоСырьяПоПодразделениямКрш.СкладДавальческогоСырья
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запасы.Ссылка.Дата КАК Период,
	|	Запасы.итсСтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	Запасы.Ссылка.Организация КАК Организация,
	|	Запасы.Номенклатура КАК Номенклатура,
	|	Запасы.Характеристика КАК Характеристика,
	|	ВЫРАЗИТЬ(Запасы.Ссылка.ДокументОснование КАК Документ.асЗаказНаряд) КАК асЗаказНаряд,
	|	СУММА(Запасы.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_ДанныеДок
	|ИЗ
	|	Документ.ПеремещениеЗапасов.Запасы КАК Запасы
	|ГДЕ
	|	Запасы.Ссылка = &Перемещение
	|	И (ВЫРАЗИТЬ(Запасы.Ссылка.ДокументОснование КАК Документ.асЗаказНаряд)) <> ЗНАЧЕНИЕ(Документ.асЗаказНаряд.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Запасы.итсСтруктурнаяЕдиница,
	|	Запасы.Характеристика,
	|	Запасы.Номенклатура,
	|	Запасы.Ссылка.Организация,
	|	ВЫРАЗИТЬ(Запасы.Ссылка.ДокументОснование КАК Документ.асЗаказНаряд),
	|	Запасы.Ссылка.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////  
	|ВЫБРАТЬ
	|	ВТ_ДанныеДок.Период КАК Период,
	|	&Перемещение КАК Регистратор,
	|	ИСТИНА КАК Активность,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВТ_ДанныеДок.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВТ_ДанныеДок.Организация КАК Организация,
	|	ВТ_ДанныеДок.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеДок.Характеристика КАК Характеристика,
	|	ВТ_ДанныеДок.асЗаказНаряд КАК асЗаказНаряд,
	|	ВТ_ДанныеДок.Количество КАК Количество
	|ИЗ
	|	ВТ_ДанныеДок КАК ВТ_ДанныеДок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.RMS_РезервыПодЗаказНаряд.Остатки(, асЗаказНаряд = &ЗаказНаряд) КАК Ост
	|		ПО ВТ_ДанныеДок.асЗаказНаряд = Ост.асЗаказНаряд
	|			И ВТ_ДанныеДок.СтруктурнаяЕдиница = Ост.СтруктурнаяЕдиница
	|			И ВТ_ДанныеДок.Номенклатура = Ост.Номенклатура
	|			И ВТ_ДанныеДок.Характеристика = Ост.Характеристика
	|			И ВТ_ДанныеДок.Организация = Ост.Организация
	|ГДЕ
	|	ЕСТЬNULL(Ост.КоличествоОстаток, 0) >= ВТ_ДанныеДок.Количество
	|	И ВТ_ДанныеДок.СтруктурнаяЕдиница В
	|			(ВЫБРАТЬ
	|				ВТСкладыДС.СкладДавальческогоСырья
	|			ИЗ
	|				ВТСкладыДС КАК ВТСкладыДС)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДвиженияРезервы = РезультатЗапроса.Выгрузить();

	Если ДвиженияРезервы.Количество() > 0 Тогда
		
		Движения.RMS_РезервыПодЗаказНаряд.Записывать = Истина;
		Движения.RMS_РезервыПодЗаказНаряд.Загрузить(ДвиженияРезервы);
		
	КонецЕсли;
	// --- Инфостарт #2666 03.08.2021
	// --- Инфостарт #2666 28.07.2021
	
КонецПроцедуры
