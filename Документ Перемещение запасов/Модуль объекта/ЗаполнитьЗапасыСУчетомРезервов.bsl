&НаСервере
Процедура ЗаполнитьЗапасыСУчетомРезервов()
	
	РезервыНоменклатуры = Новый ТаблицаЗначений;
	РезервыНоменклатуры.Колонки.Добавить("асЗаказНаряд");
	РезервыНоменклатуры.Колонки.Добавить("Номенклатура");
	РезервыНоменклатуры.Колонки.Добавить("Характеристика");
	РезервыНоменклатуры.Колонки.Добавить("РезервТекущегоЗаказНаряда");
	РезервыНоменклатуры.Колонки.Добавить("Количество");
	// +++ Инфостарт #3776 07.10.2021
	РезервыНоменклатуры.Колонки.Добавить("СкладРезерва");
	// --- Инфостарт #3776 07.10.2021
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	RMS_РезервыПодЗаказНарядОстаткиИОбороты.асЗаказНаряд КАК асЗаказНаряд,
		|	RMS_РезервыПодЗаказНарядОстаткиИОбороты.асЗаказНаряд.Подразделение КАК Подразделение,
		|	RMS_РезервыПодЗаказНарядОстаткиИОбороты.Номенклатура КАК Номенклатура,
		|	RMS_РезервыПодЗаказНарядОстаткиИОбороты.Характеристика КАК Характеристика,
		|	ВЫБОР
		|		КОГДА RMS_РезервыПодЗаказНарядОстаткиИОбороты.СтруктурнаяЕдиница.дмРаспределятьРезервЗапчастейНаСкладе
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|		ИНАЧЕ RMS_РезервыПодЗаказНарядОстаткиИОбороты.СтруктурнаяЕдиница
		|	КОНЕЦ КАК СкладУчета,
		// +++ Инфостарт #3776 07.10.2021
		|	RMS_РезервыПодЗаказНарядОстаткиИОбороты.СтруктурнаяЕдиница КАК СкладРезерва,
		// --- Инфостарт #3776 07.10.2021
		|	СУММА(RMS_РезервыПодЗаказНарядОстаткиИОбороты.КоличествоПриход - RMS_РезервыПодЗаказНарядОстаткиИОбороты.КоличествоРасход) КАК Количество,
		|	МИНИМУМ(RMS_РезервыПодЗаказНарядОстаткиИОбороты.Период) КАК ДатаРезерва
		|ПОМЕСТИТЬ ВТ_Резервы
		|ИЗ
		|	РегистрНакопления.RMS_РезервыПодЗаказНаряд.ОстаткиИОбороты(
		|			,
		|			,
		|			Секунда,
		|			,
		|			Номенклатура В (&Номенклатура)
		|				И Характеристика В (&Характеристика)
		|				И Организация = &Организация) КАК RMS_РезервыПодЗаказНарядОстаткиИОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	RMS_РезервыПодЗаказНарядОстаткиИОбороты.асЗаказНаряд,
		|	RMS_РезервыПодЗаказНарядОстаткиИОбороты.асЗаказНаряд.Подразделение,
		|	RMS_РезервыПодЗаказНарядОстаткиИОбороты.Номенклатура,
		|	RMS_РезервыПодЗаказНарядОстаткиИОбороты.Характеристика,
		// +++ Инфостарт #3776 07.10.2021
		|	RMS_РезервыПодЗаказНарядОстаткиИОбороты.СтруктурнаяЕдиница,
		// --- Инфостарт #3776 07.10.2021
		|	ВЫБОР
		|		КОГДА RMS_РезервыПодЗаказНарядОстаткиИОбороты.СтруктурнаяЕдиница.дмРаспределятьРезервЗапчастейНаСкладе
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|		ИНАЧЕ RMS_РезервыПодЗаказНарядОстаткиИОбороты.СтруктурнаяЕдиница
		|	КОНЕЦ
		|
		|ИМЕЮЩИЕ
		|	СУММА(RMS_РезервыПодЗаказНарядОстаткиИОбороты.КоличествоПриход - RMS_РезервыПодЗаказНарядОстаткиИОбороты.КоличествоРасход) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	дмСкладыДавальческогоСырьяПоПодразделениямКрш.Подразделение КАК Подразделение,
		|	ВЫБОР
		|		КОГДА дмСкладыДавальческогоСырьяПоПодразделениямКрш.СкладДавальческогоСырья.дмРаспределятьРезервЗапчастейНаСкладе
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|		ИНАЧЕ дмСкладыДавальческогоСырьяПоПодразделениямКрш.СкладДавальческогоСырья
		|	КОНЕЦ КАК СкладДавальческогоСырья
		|ИЗ
		|	РегистрСведений.дмСкладыДавальческогоСырьяПоПодразделениямКрш КАК дмСкладыДавальческогоСырьяПоПодразделениямКрш
		|
		|СГРУППИРОВАТЬ ПО
		|	дмСкладыДавальческогоСырьяПоПодразделениямКрш.Подразделение,
		|	ВЫБОР
		|		КОГДА дмСкладыДавальческогоСырьяПоПодразделениямКрш.СкладДавальческогоСырья.дмРаспределятьРезервЗапчастейНаСкладе
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|		ИНАЧЕ дмСкладыДавальческогоСырьяПоПодразделениямКрш.СкладДавальческогоСырья
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница.дмРаспределятьРезервЗапчастейНаСкладе
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|		ИНАЧЕ ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница
		|	КОНЕЦ КАК СтруктурнаяЕдиница,
		|	ЗапасыНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ЗапасыНаСкладахОстатки.Характеристика КАК Характеристика,
		|	СУММА(ЗапасыНаСкладахОстатки.КоличествоОстаток) КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ЗапасыНаСкладах.Остатки(
		|			,
		|			Номенклатура В (&Номенклатура)
		|				И Характеристика В (&Характеристика)
		|				И Организация = &Организация) КАК ЗапасыНаСкладахОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыНаСкладахОстатки.Номенклатура,
		|	ЗапасыНаСкладахОстатки.Характеристика,
		|	ВЫБОР
		|		КОГДА ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница.дмРаспределятьРезервЗапчастейНаСкладе
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|		ИНАЧЕ ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Резервы.асЗаказНаряд КАК асЗаказНаряд,
		|	ВТ_Резервы.Подразделение КАК Подразделение,
		|	ВТ_Резервы.Номенклатура КАК Номенклатура,
		|	ВТ_Резервы.Характеристика КАК Характеристика,
		|	ВТ_Резервы.СкладУчета КАК СкладУчета,
		// +++ Инфостарт #3776 07.10.2021
		|	ВТ_Резервы.СкладРезерва КАК СкладРезерва,
		// --- Инфостарт #3776 07.10.2021
		|	ВТ_Резервы.Количество КАК Количество,
		|	ВТ_Резервы.ДатаРезерва КАК ДатаРезерва
		|ИЗ
		|	ВТ_Резервы КАК ВТ_Резервы
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаРезерва
		|ИТОГИ ПО
		|	Номенклатура,
		|	Характеристика";
	
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("Номенклатура",   Запасы.Выгрузить(, "Номенклатура"));
	Запрос.УстановитьПараметр("Характеристика", Запасы.Выгрузить(, "Характеристика"));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаДавСкладов = РезультатЗапроса[1].Выгрузить();
	ТаблицаОстатков   = РезультатЗапроса[2].Выгрузить();
	ВыборкаТоваров    = РезультатЗапроса[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПустойСклад = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
	
	// Перенос полученных данных в таблицу
	Пока ВыборкаТоваров.Следующий() Цикл
		
		ВыборкаХарактеристик = ВыборкаТоваров.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаХарактеристик.Следующий() Цикл
			
			ВыборкаРезервов = ВыборкаХарактеристик.Выбрать();
			
			Пока ВыборкаРезервов.Следующий() Цикл
				
				НоваяСтрока = РезервыНоменклатуры.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРезервов);
				НоваяСтрока.РезервТекущегоЗаказНаряда = "Ожидание наличия";
				
				ОтборДавСклада = Новый Структура("Подразделение", ВыборкаРезервов.Подразделение);
				НайденныеДавСклады = ТаблицаДавСкладов.НайтиСтроки(ОтборДавСклада);
				
				КоличествоРезерва = ВыборкаРезервов.Количество;
				
				// Обработка заказов, в которых к подразделению указаны давальческие склады (один или несколько)
				Если НайденныеДавСклады.Количество() > 0 И ВыборкаРезервов.СкладУчета <> ПустойСклад Тогда
					
					ЧастичноОтгружен = Ложь;
					
					Для Каждого ДавСклад Из НайденныеДавСклады Цикл
						
						ОтборОстатка = Новый Структура;
						ОтборОстатка.Вставить("СтруктурнаяЕдиница", ДавСклад.СкладДавальческогоСырья);
						ОтборОстатка.Вставить("Номенклатура",       ВыборкаТоваров.Номенклатура);
						ОтборОстатка.Вставить("Характеристика",     ВыборкаХарактеристик.Характеристика);
						НайденныеОстатки = ТаблицаОстатков.НайтиСтроки(ОтборОстатка);
						
						Если НайденныеОстатки.Количество() > 0 И КоличествоРезерва > 0 Тогда
							
							ОстатокНаСкладе = НайденныеОстатки[0].КоличествоОстаток;
							
							Если ОстатокНаСкладе >= КоличествоРезерва Тогда
								
								НайденныеОстатки[0].КоличествоОстаток = ОстатокНаСкладе - КоличествоРезерва;
								НоваяСтрока.РезервТекущегоЗаказНаряда = "Готов к выдаче";
								
								КоличествоРезерва = 0;
								ЧастичноОтгружен = Истина;
								
							ИначеЕсли ОстатокНаСкладе < КоличествоРезерва И ОстатокНаСкладе > 0 Тогда
								
								НоваяСтрока.Количество                = НайденныеОстатки[0].КоличествоОстаток;
								НоваяСтрока.РезервТекущегоЗаказНаряда = "Частично готов к выдаче";
								НайденныеОстатки[0].КоличествоОстаток = 0;
								
								КоличествоРезерва = КоличествоРезерва - ОстатокНаСкладе;
								ЧастичноОтгружен = Истина;
								
							ИначеЕсли ОстатокНаСкладе <= 0 Тогда
								
								НоваяСтрока.Количество                = 0;
								НоваяСтрока.РезервТекущегоЗаказНаряда = "Ожидание наличия";
								
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЦикла;
					
					Если НоваяСтрока.РезервТекущегоЗаказНаряда = "Ожидание наличия" И ЧастичноОтгружен Тогда
						НоваяСтрока.РезервТекущегоЗаказНаряда = "Частично готов к выдаче";
					КонецЕсли;
					
				// Обработка заказ-нарядов без давальческих складов
				Иначе
					
					ОтборОстатка = Новый Структура;
					ОтборОстатка.Вставить("СтруктурнаяЕдиница", ВыборкаРезервов.СкладУчета);
					ОтборОстатка.Вставить("Номенклатура",       ВыборкаТоваров.Номенклатура);
					ОтборОстатка.Вставить("Характеристика",     ВыборкаХарактеристик.Характеристика);
					НайденныеОстатки = ТаблицаОстатков.НайтиСтроки(ОтборОстатка);
					
					Если НайденныеОстатки.Количество() > 0 Тогда
						
						ОстатокНаСкладе = НайденныеОстатки[0].КоличествоОстаток;
						
						Если ОстатокНаСкладе >= ВыборкаРезервов.Количество Тогда
							
							НайденныеОстатки[0].КоличествоОстаток = ОстатокНаСкладе - ВыборкаРезервов.Количество;
							НоваяСтрока.РезервТекущегоЗаказНаряда = "Готов к выдаче";
							
						ИначеЕсли ОстатокНаСкладе < ВыборкаРезервов.Количество И ОстатокНаСкладе > 0 Тогда
							
							НоваяСтрока.Количество                = НайденныеОстатки[0].КоличествоОстаток;
							НоваяСтрока.РезервТекущегоЗаказНаряда = "Частично готов к выдаче";
							НайденныеОстатки[0].КоличествоОстаток = 0;
							
						ИначеЕсли ОстатокНаСкладе <= 0 Тогда
							
							НоваяСтрока.Количество                = 0;
							НоваяСтрока.РезервТекущегоЗаказНаряда = "Ожидание наличия";
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	// +++ Инфостарт #3776 07.10.2021
	Для каждого СтрокаРезерва Из РезервыНоменклатуры Цикл
		Если СтрокаРезерва.СкладРезерва <> ПустойСклад
				И СтрокаРезерва.РезервТекущегоЗаказНаряда = "Готов к выдаче" Тогда
			
			ОтборРезерва = Новый Структура;
			ОтборРезерва.Вставить("асЗаказНаряд",   СтрокаРезерва.асЗаказНаряд);
			ОтборРезерва.Вставить("Номенклатура",   СтрокаРезерва.Номенклатура);
			ОтборРезерва.Вставить("Характеристика", СтрокаРезерва.Характеристика);
			ОтборРезерва.Вставить("СкладРезерва",   ПустойСклад);
			
			СтрокиПустыхСкладов = РезервыНоменклатуры.НайтиСтроки(ОтборРезерва);
			
			Для Каждого СтрокаПустыхСкладов Из СтрокиПустыхСкладов Цикл
				СтрокаПустыхСкладов.РезервТекущегоЗаказНаряда = "Готов к выдаче";
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	// --- Инфостарт #3776 07.10.2021
	
	// Заполнение колонки РезервТекущегоЗаказНаряда
	МассивУдаляемыхСтрок = Новый Массив;
	
	Для Каждого СтрокаЗапасов Из Запасы Цикл
		
		РезервТекущегоЗаказНаряда = "";
		
		ОтборМатериала = Новый Структура;
		ОтборМатериала.Вставить("асЗаказНаряд",   ДокументОснование);
		ОтборМатериала.Вставить("Номенклатура",   СтрокаЗапасов.Номенклатура);
		ОтборМатериала.Вставить("Характеристика", СтрокаЗапасов.Характеристика);
		
		НайденныеРезервы = РезервыНоменклатуры.НайтиСтроки(ОтборМатериала);
		
		// Вычисление итогового статуса. Если отгрузка частичная, то количество в строке уменьшается
		Для Каждого НайденныйРезерв Из НайденныеРезервы Цикл
			
			Если НайденныйРезерв.РезервТекущегоЗаказНаряда = "Готов к выдаче"
					И (РезервТекущегоЗаказНаряда = "Ожидание наличия"
					ИЛИ РезервТекущегоЗаказНаряда = "Частично готов к выдаче") Тогда
				РезервТекущегоЗаказНаряда = "Частично готов к выдаче";
			ИначеЕсли (РезервТекущегоЗаказНаряда = "Готов к выдаче"
					ИЛИ РезервТекущегоЗаказНаряда = "Частично готов к выдаче")
					И НайденныйРезерв.РезервТекущегоЗаказНаряда = "Ожидание наличия" Тогда
				РезервТекущегоЗаказНаряда = "Частично готов к выдаче";
			Иначе
				РезервТекущегоЗаказНаряда = НайденныйРезерв.РезервТекущегоЗаказНаряда;
			КонецЕсли;
			
			Если РезервТекущегоЗаказНаряда = "Частично готов к выдаче"
					И СтрокаЗапасов.Количество > НайденныйРезерв.Количество Тогда
				СтрокаЗапасов.Количество = НайденныйРезерв.Количество;
			КонецЕсли;
			
		КонецЦикла;
		
		// Удаление строк, по которым нет наличия. Вывод сообщения о других резервах
		Если РезервТекущегоЗаказНаряда = "Ожидание наличия" Тогда
			
			ОтборМатериала = Новый Структура;
			ОтборМатериала.Вставить("Номенклатура",              СтрокаЗапасов.Номенклатура);
			ОтборМатериала.Вставить("Характеристика",            СтрокаЗапасов.Характеристика);
			ОтборМатериала.Вставить("РезервТекущегоЗаказНаряда", "Готов к выдаче");
			
			НайденныеРезервы = РезервыНоменклатуры.НайтиСтроки(ОтборМатериала);
			
			Для Каждого НайденныйРезерв Из НайденныеРезервы Цикл
				Если НайденныйРезерв.асЗаказНаряд <> ДокументОснование Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон(
						НСтр("ru = 'Номенклатура - %1 в резерве под заказ - %2'"),
						НайденныйРезерв.Номенклатура,
						НайденныйРезерв.асЗаказНаряд));
				КонецЕсли;
			КонецЦикла;
			
			МассивУдаляемыхСтрок.Добавить(СтрокаЗапасов);
			
		КонецЕсли;
		
	КонецЦикла;
		
	// --- Инфостарт #3776 31.08.2021
	
КонецПроцедуры

