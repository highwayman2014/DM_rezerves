//++ DEV1C-2786, Седов М.В.
Процедура СформироватьДвиженияВРаботеЛКМ(Отказ, РежимПроведения)
	
	// Делаем движения-приход.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Период", ЭтотОбъект.Дата);
	ЗАпрос.УстановитьПараметр("ВидОперацииПеремещения", Перечисления.ВидыОперацийПеремещениеЗапасов.асПередачаВРемонт);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Док.Номенклатура КАК Номенклатура,
	|	Док.Характеристика КАК Характеристика,
	|	Док.Ссылка.Организация КАК Организация,
	|	Док.Ссылка КАК Ссылка,
	|	СУММА(Док.Количество) КАК Количество,
	|	ПеремещениеЗапасовЗапасы.итсСтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	// +++ Инфостарт #2666 14.07.2021 Отмена доработок, закупки запчастей не попадают в резервы
	|ПОМЕСТИТЬ ЗаказНаряд
	// --- Инфостарт #2666 14.07.2021
	|ИЗ
	|	Документ.асЗаказНаряд.Запасы КАК Док
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеЗапасов.Запасы КАК ПеремещениеЗапасовЗапасы
	|		ПО ВЫРАЗИТЬ(ПеремещениеЗапасовЗапасы.Ссылка.ДокументОснование КАК Документ.асЗаказНаряд) = Док.Ссылка
	|ГДЕ
	|	Док.Ссылка = &Регистратор
	|	И Док.дмРезервировать = ИСТИНА
	|	И ПеремещениеЗапасовЗапасы.Ссылка.ВидОперации = &ВидОперацииПеремещения
	|
	|СГРУППИРОВАТЬ ПО
	|	Док.Ссылка,
	|	Док.Номенклатура,
	|	Док.Характеристика,
	|	Док.Ссылка.Организация,
	|	ПеремещениеЗапасовЗапасы.итсСтруктурнаяЕдиница
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация КАК Организация,
	|	Т.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	Т.Номенклатура КАК Номенклатура,
	|	Т.Характеристика КАК Характеристика,
	|	СУММА(Т.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВТ_Ост
	|ИЗ
	|	(ВЫБРАТЬ
	|		Остатки.Номенклатура КАК Номенклатура,
	|		Остатки.Характеристика КАК Характеристика,
	|		Остатки.Организация КАК Организация,
	|		Остатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		Остатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.ЗапасыНаСкладах.Остатки(
	|				,
	|				(Номенклатура, Характеристика, Организация, СтруктурнаяЕдиница) В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						Т.Номенклатура КАК Номенклатура,
	|						Т.Характеристика КАК Характеристика,
	|						Т.Организация КАК Организация,
	|						Т.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	|					ИЗ
	|						ЗаказНаряд КАК Т)) КАК Остатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Резервы.Номенклатура,
	|		Резервы.Характеристика,
	|		Резервы.Организация,
	|		Резервы.СтруктурнаяЕдиница,
	|		ВЫБОР
	|			КОГДА Резервы.КоличествоОстаток > 0
	|				ТОГДА -Резервы.КоличествоОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.RMS_РезервыПодЗаказНаряд.Остатки(
	|				,
	|				асЗаказНаряд <> &Регистратор
	|					И (Номенклатура, Характеристика, Организация, СтруктурнаяЕдиница) В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							Т.Номенклатура КАК Номенклатура,
	|							Т.Характеристика КАК Характеристика,
	|							Т.Организация КАК Организация,
	|							Т.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	|						ИЗ
	|							ЗаказНаряд КАК Т)) КАК Резервы) КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.СтруктурнаяЕдиница,
	|	Т.Номенклатура,
	|	Т.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Номенклатура КАК Номенклатура,
	|	Док.Характеристика КАК Характеристика,
	|	Док.Организация КАК Организация,
	|	Док.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	Док.Количество КАК КоличествоТребуется,
	|	Док.Количество КАК КоличествоЗарезервировать,
	|	ЕСТЬNULL(Ост.КоличествоОстаток, 0) КАК КоличествоОстаток
	|ПОМЕСТИТЬ Резерв
	|ИЗ
	|	ЗаказНаряд КАК Док
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Ост КАК Ост
	|		ПО Док.Номенклатура = Ост.Номенклатура
	|			И Док.Характеристика = Ост.Характеристика
	|			И Док.Организация = Ост.Организация
	|			И Док.СтруктурнаяЕдиница = Ост.СтруктурнаяЕдиница
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	Р.Ссылка КАК Регистратор,
	|	ИСТИНА КАК Активность,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Р.Номенклатура КАК Номенклатура,
	|	Р.Характеристика КАК Характеристика,
	|	Р.Организация КАК Организация,
	|	Р.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	Р.Ссылка КАК асЗаказНаряд,
	|	Р.КоличествоЗарезервировать КАК Количество
	|ИЗ
	|	Резерв КАК Р
	|ГДЕ
	|	Р.КоличествоЗарезервировать > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Р.Номенклатура КАК Номенклатура,
	|	Р.Характеристика КАК Характеристика,
	|	Р.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	Р.КоличествоТребуется КАК КоличествоТребуется,
	|	Р.КоличествоЗарезервировать КАК КоличествоЗарезервировать,
	|	Р.КоличествоОстаток КАК КоличествоНехватка,
	|	Р.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияПредставление,
	|	Р.Номенклатура.Представление КАК НоменклатураПредставление
	|ИЗ
	|	Резерв КАК Р
	|ГДЕ
	|	Р.КоличествоОстаток < 0";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ДвиженияРезервы	= МассивРезультатов[ МассивРезультатов.Количество() - 2 ].Выгрузить();
	
	Движения.RMS_РезервыПодЗаказНаряд.Записывать = Истина;
	Движения.RMS_РезервыПодЗаказНаряд.Загрузить(ДвиженияРезервы);				
	
	Выборка = МассивРезультатов[ МассивРезультатов.Количество() - 1 ].Выбрать();	
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтрШаблон(
			"Товара <%1> не достаточно на складе <%2> для резервирования:   
			|- необходимо: %3; зарезервировано: %4; нехватка: %5",
			Выборка.НоменклатураПредставление,
			Выборка.СтруктурнаяЕдиница,
			ФмтКоличество(Выборка.КоличествоТребуется, Выборка.ЕдиницаИзмеренияПредставление),
			ФмтКоличество(Выборка.КоличествоЗарезервировать, Выборка.ЕдиницаИзмеренияПредставление),
			ФмтКоличество(Выборка.КоличествоНехватка, Выборка.ЕдиницаИзмеренияПредставление)
		);
		
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект.Ссылка, ТекстСообщения);
		
	КонецЦикла;
КонецПроцедуры
//-- DEV1C-2786, Седов М.В.

