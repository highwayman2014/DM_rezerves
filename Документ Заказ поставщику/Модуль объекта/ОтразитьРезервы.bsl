// +++ Инфостарт #2666 02.08.2021
Процедура ОтразитьРезервы(ДополнительныеСвойства, Движения, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(дм_НомерЗаказНаряда) И ЗначениеЗаполнено(дм_ДатаЗаказНаряда)
			И дмЗаказНаДавальческоеСырье Тогда
		
		ТаблицаПодЗаказНарядКарсервис = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПодЗаказНарядКарсервис;
		
		// +++ Инфостарт #2666 04.08.2021 Движения расхода объединяются с приходом с вычетом количества
		МассивУдаляемыхСтрок = Новый Массив;
		
		Для каждого СтрокаДвижения Из ТаблицаПодЗаказНарядКарсервис Цикл
			Если СтрокаДвижения.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
				
				ПараметрыОтбораСтрок = Новый Структура;
				ПараметрыОтбораСтрок.Вставить("ВидДвижения",        ВидДвиженияНакопления.Приход);
				ПараметрыОтбораСтрок.Вставить("Организация",        СтрокаДвижения.Организация);
				ПараметрыОтбораСтрок.Вставить("Номенклатура",       СтрокаДвижения.Номенклатура);
				ПараметрыОтбораСтрок.Вставить("Характеристика",     СтрокаДвижения.Характеристика);
				ПараметрыОтбораСтрок.Вставить("СтруктурнаяЕдиница", СтрокаДвижения.СтруктурнаяЕдиница);
				ПараметрыОтбораСтрок.Вставить("асЗаказНаряд",       СтрокаДвижения.асЗаказНаряд);
				
				НайденныеСтроки = ТаблицаПодЗаказНарядКарсервис.НайтиСтроки(ПараметрыОтбораСтрок);
				
				КоличествоРасход = СтрокаДвижения.Количество;
				
				КоличествоНайденныхСтрок = НайденныеСтроки.Количество();
				
				Для Сч = 1 По КоличествоНайденныхСтрок Цикл
					
					ТекущаяСтрока = НайденныеСтроки[КоличествоНайденныхСтрок - Сч];
					
					Если ТекущаяСтрока.Количество >= КоличествоРасход И КоличествоРасход > 0 Тогда
						
						ТекущаяСтрока.Количество = ТекущаяСтрока.Количество - КоличествоРасход;
						КоличествоРасход = 0;
						
						МассивУдаляемыхСтрок.Добавить(СтрокаДвижения);
						
					ИначеЕсли ТекущаяСтрока.Количество < КоличествоРасход И КоличествоРасход > 0 Тогда
						
						КоличествоРасход = КоличествоРасход - ТекущаяСтрока.Количество;
						ТекущаяСтрока.Количество = 0;
						
					КонецЕсли;
					
					Если ТекущаяСтрока.Количество = 0 Тогда
						МассивУдаляемыхСтрок.Добавить(ТекущаяСтрока);
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого УдаляемаяСтрока Из МассивУдаляемыхСтрок Цикл
			ТаблицаПодЗаказНарядКарсервис.Удалить(УдаляемаяСтрока);
		КонецЦикла;
		// --- Инфостарт #2666 04.08.2021
		
	Иначе
		
		ТаблицаПодЗаказНарядКарсервис = Новый ТаблицаЗначений;
		
	КонецЕсли;
	
	ДвиженияРезервыПодЗаказНаряд = Движения.RMS_РезервыПодЗаказНаряд;
	ДвиженияРезервыПодЗаказНаряд.Записывать = Истина;
	ДвиженияРезервыПодЗаказНаряд.Загрузить(ТаблицаПодЗаказНарядКарсервис);
	
КонецПроцедуры
// --- Инфостарт #2666 02.08.2021

